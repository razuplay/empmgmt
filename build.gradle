plugins {
	id 'java'
	id 'org.springframework.boot' version '1.5.1.RELEASE'
}

repositories {
	mavenCentral()
	maven {
		url "http://www.smartclient.com/maven2"
	}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	main.java.srcDirs "src/main/java", "src/main/gwt"
}

dependencies {
	compile "org.springframework.boot:spring-boot-starter-web"
	compile "org.springframework.boot:spring-boot-starter-data-jpa"

	runtime "com.h2database:h2"

	compileOnly "com.google.gwt:gwt-user:$gwtVersion"
	compileOnly "com.google.gwt:gwt-dev:$gwtVersion"
	compileOnly "com.googlecode.mvp4g:mvp4g:$mvp4gVersion"
	compileOnly "com.smartgwt:smartgwt:$smartgwtVersion"
	compileOnly "com.smartgwt:smartgwt-skins:$smartgwtVersion"
}

task compileGwt(dependsOn: classes, type: JavaExec) {
	ext.buildDir = "${sourceSets.main.output.resourcesDir}/static"

	doFirst {
		file(buildDir).mkdirs()
	}

	main = 'com.google.gwt.dev.Compiler'

	classpath {
		[
				sourceSets.main.java.srcDirs,
				sourceSets.main.output.resourcesDir,
				sourceSets.main.output.classesDir,
				sourceSets.main.compileClasspath,
		]
	}

	args = [
			'empmgmt.Root',
			'-war', buildDir,
			'-logLevel', 'INFO',
			'-localWorkers', '4',
			'-compileReport',
			'-extra', "${project.buildDir}/extra",
			'-style', 'OBF',
			'-optimize', '7'
	]

	maxHeapSize = '512M'
}

jar.dependsOn compileGwt
bootRun.dependsOn compileGwt